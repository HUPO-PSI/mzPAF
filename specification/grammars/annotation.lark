DIGIT : "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"

SYMBOL_WITHOUT_CLOSING_BRACE : "[" | "{" | "}" | "(" | ")" | "<" | ">"
       | "'" | "\"" | "=" | "|" | "." | "," | ";" | "_" | "-" | "+"

SYMBOL : SYMBOL_WITHOUT_CLOSING_BRACE | "]"

UPPER_CASE_LETTER : "A" | "B" | "C" | "D" | "E" | "F" | "G"
       | "H" | "I" | "J" | "K" | "L" | "M" | "N"
       | "O" | "P" | "Q" | "R" | "S" | "T" | "U"
       | "V" | "W" | "X" | "Y" | "Z"

LOWER_CASE_LETTER :  "a" | "b"
       | "c" | "d" | "e" | "f" | "g" | "h" | "i"
       | "j" | "k" | "l" | "m" | "n" | "o" | "p"
       | "q" | "r" | "s" | "t" | "u" | "v" | "w"
       | "x" | "y" | "z"

LETTER : UPPER_CASE_LETTER | LOWER_CASE_LETTER

CHARACTER : LETTER | DIGIT | SYMBOL

CONTENT_IN_BRACES : LETTER | DIGIT | SYMBOL_WITHOUT_CLOSING_BRACE | " "

BRACE_ENCLOSED_CONTENT : "[" (CONTENT_IN_BRACES)+ ("[" (CONTENT_IN_BRACES | " ")+ "]")? "]"

ANALYTE_REFERENCE : (CHARACTER)+

is_auxiliary : "&"

analyte_reference_ : (ANALYTE_REFERENCE) "@"

PEPTIDE_SERIES_SYMBOL : "da" | "db" | "wa" | "wb" | "a" | "x" | "b" | "y" | "c" | "z" | "d" | "v" | "w"

ORDINAL : (DIGIT)+

NUMBER : DIGIT ("." (DIGIT)+)?

AMINO_ACID : UPPER_CASE_LETTER

ELEMENT : "Ac" | "Ag" | "Al" | "Am" | "Ar" | "As" | "At" | "Au" | "B" | "Ba" | "Be" | "Bh" | "Bi" | "Bk" | "Br" | "C" | "Ca" | "Cd" |
          "Ce" | "Cf" | "Cl" | "Cm" | "Cn" | "Co" | "Cr" | "Cs" | "Cu" | "Db" | "Ds" | "Dy" | "Er" | "Es" | "Eu" | "F" | "Fe" | "Fm" |
          "Fr" | "Ga" | "Gd" | "Ge" | "H" | "H+" | "He" | "Hf" | "Hg" | "Ho" | "Hs" | "I" | "In" | "Ir" | "K" | "Kr" | "La" | "Li" |
          "Lr" | "Lu" | "Md" | "Mg" | "Mn" | "Mo" | "Mt" | "N" | "Na" | "Nb" | "Nd" | "Ne" | "Ni" | "No" | "Np" | "O" | "Os" | "P" |
          "Pa" | "Pb" | "Pd" | "Pm" | "Po" | "Pr" | "Pt" | "Pu" | "Ra" | "Rb" | "Re" | "Rf" | "Rg" | "Rh" | "Rn" | "Ru" | "S" | "Sb" |
          "Sc" | "Se" | "Sg" | "Si" | "Sm" | "Sn" | "Sr" | "Ta" | "Tb" | "Tc" | "Te" | "Th" | "Ti" | "Tl" | "Tm" | "U" | "Uuh" | "Uuo" |
          "Uup" | "Uuq" | "Uus" | "Uut" | "V" | "W" | "Xe" | "Y" | "Yb" | "Zn" | "Zr"

ATOM_COUNT : UPPER_CASE_LETTER LOWER_CASE_LETTER? "-"? ORDINAL*

CHEMICAL_FORMULA : ATOM_COUNT+

REPORTER_LABEL : BRACE_ENCLOSED_CONTENT

SIGN : ("+" | "-")

SMILES : /^([^J][0-9BCOHNSOPrIFla@+\-\[\]\(\)\\\/%=#$]{6,})$/i

neutral_loss : SIGN (CHEMICAL_FORMULA | BRACE_ENCLOSED_CONTENT)

isotope : SIGN (ORDINAL* "i") (ORDINAL+ ELEMENT)? isotope?

charge : "^" SIGN? ORDINAL

adducts : BRACE_ENCLOSED_CONTENT

PPM : "ppm"

mass_error : "/" NUMBER PPM?

annotation_confidence : "*" NUMBER

annotation_qualifiers : mass_error? annotation_confidence?

series_peptide : (PEPTIDE_SERIES_SYMBOL) (ORDINAL+)

series_internal : "m" ORDINAL+ ":" ORDINAL+

series_immonium : "I" AMINO_ACID BRACE_ENCLOSED_CONTENT?

series_reporter : "r" REPORTER_LABEL

series_formula : "f" "{" CHEMICAL_FORMULA "}"

series_named_compound : "_" "{" CHARACTER+ "}"

series_smiles : "s" "{" SMILES+ "}"

series_unknown : "?" ORDINAL?

ion_series : series_peptide | series_immonium | series_internal | series_reporter | series_formula | series_named_compound | series_smiles | series_unknown

analyte_reference_ion_series : (analyte_reference_ ion_series) | ion_series

annotation : is_auxiliary? analyte_reference_? ion_series neutral_loss* isotope? adducts? charge? annotation_qualifiers?
